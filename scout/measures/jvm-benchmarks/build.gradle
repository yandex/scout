plugins {
    id 'application'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.kapt'
    id 'graph-generator-plugin'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

application {
    mainClassName = "scout.benchmark.LauncherKt"
    tasks.named("run").get().workingDir = rootProject.projectDir
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.openjdk.jmh:jmh-core:1.36"
    kapt "org.openjdk.jmh:jmh-generator-annprocess:1.36"

    // scout
    implementation scout_core_module
    // koin
    implementation 'io.insert-koin:koin-core:3.4.3'
    // dagger
    implementation 'com.google.dagger:dagger:2.47'
    kapt 'com.google.dagger:dagger-compiler:2.47'
    // kodein
    implementation 'org.kodein.di:kodein-di:7.19.0'
    // Katana
    implementation "org.rewedigital.katana:katana-core:1.15.0"
}

sourceSets {
    main {
        kotlin {
            //noinspection GroovyAssignabilityCheck
            srcDir('build/generated/sources/graphGenerator')
        }
    }
}

compileKotlin {
    dependsOn(generateGraphs)
}

graphs {
    generate("g5nodes").nodes(5).modules(1, 3)
    generate("g25nodes").nodes(25).modules(5, 10)
    generate("g125nodes").nodes(125).modules(15, 50)
}